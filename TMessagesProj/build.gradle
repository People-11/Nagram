apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "io.objectbox"

def verName = APP_VERSION_NAME
def verCode = 1226


def officialVer = APP_VERSION_NAME
def officialCode = APP_VERSION_CODE

def serviceAccountCredentialsFile = rootProject.file("service_account_credentials.json")

def beta = verName.contains("preview")

if (serviceAccountCredentialsFile.isFile()) {
    setupPlay(beta)
    play.serviceAccountCredentials = serviceAccountCredentialsFile
} else if (System.getenv().containsKey("ANDROID_PUBLISHER_CREDENTIALS")) {
    setupPlay(beta)
}

void setupPlay(boolean beta) {
    apply plugin: "com.github.triplet.play"
    play {
        track = beta ? "beta" : "production"
        defaultToAppBundles = true
    }
}

configurations {
    compile.exclude module: "support-v4"
}

def keystorePwd = null
def alias = null
def pwd = null
def nagram_build_args = System.getenv("NAGRAM_BUILD_ARGS")
def disableCMakeRelWithDebInfo = System.getenv("COMPILE_NATIVE") == null

Properties properties
def base64 = System.getenv("LOCAL_PROPERTIES")
if (base64 != null && !base64.isBlank()) {
    properties = new Properties()
    properties.load(new ByteArrayInputStream(Base64.decoder.decode(base64)))
} else if (project.rootProject.file("local.properties").exists()) {
    properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

if (properties != null) {
    keystorePwd = properties.getProperty("KEYSTORE_PASS")
    alias = properties.getProperty("ALIAS_NAME")
    pwd = properties.getProperty("ALIAS_PASS")
    nagram_build_args = properties.getProperty("NAGRAM_BUILD_ARGS", nagram_build_args)
}

keystorePwd = keystorePwd ?: System.getenv("KEYSTORE_PASS")
alias = alias ?: System.getenv("ALIAS_NAME")
pwd = pwd ?: System.getenv("ALIAS_PASS")

def targetTask = ""
if (!gradle.startParameter.taskNames.isEmpty()) {
    if (gradle.startParameter.taskNames.size == 1) {
        targetTask = gradle.startParameter.taskNames[0].toLowerCase()
    }
}

def nativeTarget = System.getenv("NATIVE_TARGET")
if (nativeTarget == null) nativeTarget = ""

android {
    compileSdkVersion 35
    buildToolsVersion '35.0.0'
    ndkVersion rootProject.ext.ndkVersion // 26.3.11579264

    defaultConfig.applicationId = "xyz.nextalone.nagram"

    sourceSets {

        main {
            assets.srcDirs = ["src/main/assets"]
        }

        debug {
            java {
                srcDirs "src/main/java"
            }
            jniLibs {
                srcDir "src/main/libs"
            }
        }

        release {
            java {
                srcDirs "src/main/java"
            }
            jniLibs {
                srcDir "src/main/libs"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "jni/CMakeLists.txt"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'BlockedPrivateApi'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled true
    }

    splits {

        abi {
            if (targetTask.contains("fdroid")) {
                enable false
                universalApk true
            } else {
                enable true
                reset()
                universalApk false
                if (!nativeTarget.isBlank()) {
                    include nativeTarget
                } else {
                    include 'arm64-v8a'
                }
            }
        }

    }

    defaultConfig {
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 35

        versionName verName
        versionCode verCode

        def appId = "11535358"
        def appHash = "33d372962fadb01df47e6ceed4e33cd6"

        //obtain your own keys at https://core.telegram.org/api/obtaining_api_id

        if (properties != null) {
            appId = properties.getProperty("TELEGRAM_APP_ID") ?: System.getenv("TELEGRAM_APP_ID") ?: appId
            appHash = properties.getProperty("TELEGRAM_APP_HASH") ?: System.getenv("TELEGRAM_APP_HASH") ?: appHash
        }

        buildConfigField "String", "BUILD_VERSION_STRING", "\"" + verName + "\""
        buildConfigField "long", "BUILD_TIMESTAMP", APP_BUILD_TIMESTAMP + ""
        buildConfigField "String", "OFFICIAL_VERSION", "\"" + officialVer + "\""
        buildConfigField "int", "OFFICIAL_VERSION_CODE", officialCode + ""
        buildConfigField "int", "APP_ID", appId
        buildConfigField "String", "APP_HASH", "\"" + appHash + "\""

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21' //, '-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON'
            }
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    signingConfigs {
        release {
            storeFile project.file("release.keystore")
            storePassword keystorePwd
            keyAlias alias
            keyPassword pwd
        }
    }

    buildTypes {
        debug {
            isDefault true
            debuggable true
            jniDebuggable true
            multiDexEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }

        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            matchingFallbacks = ["release", "debug"]
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "version"

    packagingOptions {
        jniLibs {
            excludes += ['/fabric/**', '/META-INF/native-image/**']
            useLegacyPackaging true
        }
        resources {
            excludes += ['/fabric/**', '/META-INF/*.version', '/META-INF/*.kotlin_module', '/META-INF/native-image/**', '/builddef.lst', '/*.txt', '/DebugProbesKt.bin', '/okhttp3/internal/publicsuffix/NOTICE']
            excludes += ["META-INF/INDEX.LIST"]  // hutool
        }

        def lib = "libtmessages.*.so"
        pickFirst "lib/armeabi-v7a/$lib"
        pickFirst "lib/arm64-v8a/$lib"
    }

    buildFeatures {
        buildConfig true
    }

    tasks.configureEach { task ->
        if (((task.name.endsWith("Ndk") || task.name.startsWith("generateJsonModel") || task.name.startsWith("externalNativeBuild"))) && !task.name.contains("Debug")) {
            task.enabled = false
        }
        if (task.name.contains("uploadCrashlyticsMappingFile")) {
            enabled = false
        }
        if (disableCMakeRelWithDebInfo && task.name.contains("CMakeRelWithDebInfo")) {
            enabled = false
        }
    }

    tasks.configureEach { task ->
        if (task.name.startsWith("uploadCrashlyticsMappingFile")) {
            task.enabled = false
        } else if (task.name.contains("Crashlytics") && task.name.contains("NoGcm")) {
            task.enabled = false
        } else if (task.name.endsWith("GoogleServices") && task.name.contains("NoGcm")) {
            task.enabled = false
        } else if (task.name.contains("buildCMakeDebug") && nagram_build_args != null && nagram_build_args.contains("skip_buildCMakeDebug")) {
            task.enabled = false
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            String gramName = "Nagram"
            String first = String.format("%s-v%s(%s)", gramName, versionName, versionCode)
            String name = outputFileName.replace("TMessagesProj", first)
            name = name.replace("-release", "")
            outputFileName = name
        }
    }

    namespace "org.telegram.messenger"
}

def fcmVersion = "23.4.1"
def crashlyticsVersion = "18.6.2"
def playCoreVersion = "1.10.3"

dependencies {
    implementation 'androidx.fragment:fragment:1.8.9'
    implementation "androidx.core:core-ktx:1.16.0"
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "androidx.exifinterface:exifinterface:1.3.7"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.sharetarget:sharetarget:1.2.0"
    implementation "androidx.interpolator:interpolator:1.0.0"
    implementation 'androidx.biometric:biometric:1.1.0'

    implementation ('com.google.android.gms:play-services-cast-framework:21.4.0') {
        exclude group: 'androidx.recyclerview', module: "recyclerview"
    }
    implementation ("androidx.mediarouter:mediarouter:1.7.0") {
        exclude group: 'androidx.recyclerview', module: "recyclerview"
    }
    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    compileOnly 'org.checkerframework:checker-qual:2.5.2'
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.0'
    implementation "com.google.firebase:firebase-messaging:$fcmVersion"
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-auth:20.4.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-wearable:18.0.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.android.gms:play-services-wallet:19.1.0'
    implementation "com.googlecode.mp4parser:isoparser:1.0.6"
    implementation 'com.stripe:stripe-android:2.0.2'
    // add for auto translate
    implementation 'com.google.mlkit:language-id:17.0.6'
    implementation 'com.android.billingclient:billing:7.1.1'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'com.google.android.play:integrity:1.3.0'

    implementation 'com.google.android.gms:play-services-mlkit-subject-segmentation:16.0.0-beta1'
    implementation 'com.google.android.gms:play-services-mlkit-image-labeling:16.0.8'

    implementation 'co.nstant.in:cbor:0.8'
    implementation "androidx.credentials:credentials:1.6.0-alpha04"
    implementation "androidx.credentials:credentials-play-services-auth:1.6.0-alpha04"

//    implementation 'com.google.mlkit:translate:17.0.3'

//    constraints {
//        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
//            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
//        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
//    }
    implementation 'com.google.android.recaptcha:recaptcha:18.4.0'

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "org.robolectric:robolectric:4.5.1"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}
dependencies {
    implementation "androidx.browser:browser:1.8.0"
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0"
    // OSM
    implementation "org.osmdroid:osmdroid-android:6.1.20"
    // okhttp
    implementation "com.squareup.okhttp3:okhttp:5.3.2"
    implementation 'dnsjava:dnsjava:3.6.3'

    implementation "cn.hutool:hutool-core:5.8.42"
    implementation "cn.hutool:hutool-crypto:5.8.42"
    implementation 'cn.hutool:hutool-http:5.8.42'
    implementation "com.jakewharton:process-phoenix:3.0.0"
    // openpgp
    implementation project(":openpgp-api")

    compileOnly fileTree("libs")

    compileOnly "com.google.android.play:core:$playCoreVersion"
    // add for undo and redo
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:6.1'
    // add splash screen
    implementation("androidx.core:core-splashscreen:1.2.0")
    // add for emoji
    implementation 'com.jaredrummler:truetypeparser-light:1.0.0'
    // add for up
    implementation 'org.unifiedpush.android:connector:3.1.2'
}

apply plugin: "com.google.gms.google-services"
